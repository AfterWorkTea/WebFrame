<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method="xml" omit-xml-declaration="yes"/>
    <xsl:template match="/data">
        <script>
            function onGroupChange() {
                document.getElementById("table").submit();
            }

            function LinkToAnimal(name, id) {
				var inHTML;
				var linkToForm = document.getElementById("linkToForm");
				inHTML = linkToForm.innerHTML;
				linkToForm.innerHTML = inHTML + '<input type="hidden" id="parm" name="parm" value="' + id + '"/>';
				linkToForm.page.value = name;
				linkToForm.submit();
			}

			function onSortClick(sort) {
				var tableForm = document.getElementById("table");
				tableForm.sort.value = sort;
				tableForm.submit();
            }
        </script>
        <fieldset class="center">
            <legend>Zoo</legend>
            <div align="center">
                <form id="table" action="#" method="post">
                    <input type="hidden" name="sort" value="_sort_"/>
                    <input type="hidden" id="page" name="page" value="zoo"/>
                    <xsl:apply-templates select="groups"/>
                    <xsl:apply-templates select="table"/>
                    <div align="left">
                        <xsl:apply-templates select="buttons"/>
                    </div>
                </form>
            </div>
        </fieldset>
    </xsl:template>
    <!-- Format select list -->
    <xsl:template match="/data/groups">
        <div align="left">
        <p class="edit">Select:
            <select id="groupSelect" list="groups" name="gid" onchange="onGroupChange()">
                <option value="0">All Groups</option>
                <xsl:apply-templates select="group"/>
            </select>
        </p>
        </div>
    </xsl:template>
    <!-- Select list entries from DB -->
    <xsl:template match="/data/groups/group">
        <xsl:choose>
            <xsl:when test="@id = '_gid_'">
                <option value="{@id}" selected="selected"><xsl:value-of select="."/></option>
            </xsl:when>
            <xsl:otherwise>
                <option value="{@id}"><xsl:value-of select="."/></option>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
   <!-- Foramt Table -->
   <xsl:template match="/data/table">
        <table style="width:100%">
            <xsl:apply-templates select="columns"/>
            <xsl:apply-templates select="zoo"/>
        </table>
   </xsl:template>
   <!-- Table colums -->
   <xsl:template match="/data/table/columns">
        <tr>
            <xsl:apply-templates select="entry"/>
        </tr>
   </xsl:template>
   <!-- Table colums from DB -->
   <xsl:template match="/data/table/columns/entry">
        <xsl:choose>
            <xsl:when test="sort = '_sort_'">
                <th>
					<a class="tableHeaderDisabled" href="#" disabled="disabled">
						<xsl:value-of select="name"/>&#160;&#9660;
					</a>
                </th>
            </xsl:when>
            <xsl:otherwise>
                <th>
					<a class="tableHeader" href="#" onclick="onSortClick('{sort}')" >
						<xsl:value-of select="name"/>&#160;&#9654;
					</a>
                </th>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
    <xsl:template match="/data/table/zoo">
        <xsl:apply-templates select="animal"/>
    </xsl:template>
    <!-- Table body from DB -->
    <xsl:template match="/data/table/zoo/animal">
        <tr>
            <xsl:choose>
                <xsl:when test="(position() mod 2) = 0">
                    <xsl:attribute name="class">even</xsl:attribute>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:attribute name="class">odd</xsl:attribute>
                </xsl:otherwise>
            </xsl:choose>
            <td><xsl:value-of select="."/></td>
            <td class="center">
                <xsl:choose>
                    <xsl:when test="@group='BIRD'">
                        <a href="#" target="_self" id="linkBird" onclick="LinkToAnimal('bird', '{@id}');return false;">
                            <img src="pic/b.png" height="20" width="20" title="Bird" />
                        </a>
                    </xsl:when>
                    <xsl:when test="@group='MAMMAL'">
                        <a href="#" target="_self" id="linkMammal" onclick="LinkToAnimal('mammal', '{@id}');return false;">
                            <img src="pic/m.png" height="20" width="20" title="Mammal" />
                        </a>
                    </xsl:when>
                    <xsl:when test="@group='REPTILE'">
                        <a href="#" target="_self" id="linkReptile" onclick="LinkToAnimal('reptile', '{@id}');return false;">
                            <img src="pic/r.png" height="20" width="20" title="Reptile" />
                        </a>
                    </xsl:when>
                </xsl:choose>
           </td>
           <td class="right"><xsl:value-of select="@count"/></td>
           <td class="right"><xsl:value-of select="@len"/></td>
       </tr>
    </xsl:template>
    <!-- Foramt buttons -->
    <xsl:template match="/data/buttons">
        <p class="edit">
            <xsl:choose>
                <xsl:when test="@from &gt; 0">
                    <button type="submit" name="button_back" value="Back">&#9664;</button>
                    <input type="hidden" name="BCK" value="{@prev}" />
                </xsl:when>
                <xsl:otherwise>
                    <button type="submit" name="button_back" value="Back" disabled="disabled">&#9664;</button>
                </xsl:otherwise>
            </xsl:choose>
            records <xsl:value-of select="@from+1"/> - <xsl:value-of select="@to"/> / <xsl:value-of select="@count"/>
            <xsl:choose>
                <xsl:when test="@more &gt; 0">
                    <button type="submit" name="button_next" value="Next">&#9654;</button>
                    <input type="hidden" name="FRD" value="{@next}" />
                </xsl:when>
                <xsl:otherwise>
                    <button type="submit" name="button_next" value="Next" disabled="disabled">&#9654;</button>
                </xsl:otherwise>
           </xsl:choose>
        </p>
    </xsl:template>
</xsl:stylesheet>
